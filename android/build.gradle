// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.32"
    ext.flipper_version = '0.84.0'
    ext.minSdkVersion = 15
    ext.targetSdkVersion = 29

    repositories {
        jcenter {
            content {
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
            }
        }
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.14.2'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.30'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
    id 'io.gitlab.arturbosch.detekt' version '1.16.0'
}

tasks.detekt.jvmTarget = "1.8"
detekt {
    buildUponDefaultConfig = true // preconfigure defaults
    allRules = true // activate all available (even unstable) rules.

    reports {
        xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task addKtLintStyle(type: JavaExec, group: "formatting") {
    description = "Adds The KtLint Style To Your IDE"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "applyToIDEA", "-y"
    //Point to the root directory because this task needs access to the .idea directory
    workingDir(getRootDir())
}

task clean(type: Delete) {
    delete rootProject.buildDir
}